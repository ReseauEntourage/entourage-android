apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'sonar-runner'

def versionMajor = 1
def versionMinor = 1
def versionPatch = 'git rev-list HEAD --count'.execute([], project.rootDir).text.trim().toInteger()

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')


    apt 'com.google.dagger:dagger-compiler:2.0.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'



    compile files('libs/FlurryAnalytics-6.3.1.jar')

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:support-annotations:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile 'com.google.android.gms:play-services-location:8.4.0'
    compile 'com.google.android.gms:play-services-ads:8.4.0'
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    compile 'com.google.maps.android:android-maps-utils:0.4'
    compile 'com.google.dagger:dagger:2.0.1'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.0.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta4'
    compile 'com.squareup:tape:1.2.3'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'jp.wasabeef:picasso-transformations:1.0.5'
    compile 'com.github.clans:fab:1.6.2'
    compile 'net.danlew:android.joda:2.7.1'
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId 'social.entourage.android'
        minSdkVersion 15
        targetSdkVersion 23
        versionCode versionMajor * 100000 + versionMinor * 10000 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField 'String', 'ENTOURAGE_URL', '"https://entourage-back-preprod.herokuapp.com/api/v1/"'
        buildConfigField 'String', 'FLURRY_API_KEY', "\"${flurryApiKey}\""
    }

    signingConfigs {
        googleplay {
            keyAlias 'googleplay'
            keyPassword entourageKeystorePassword
            storeFile file('../keystore/googleplay-keystore.jks')
            storePassword entourageKeystorePassword
        }

        debug {
            storeFile file('../keystore/debug.keystore')
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.googleplay
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            buildConfigField 'String', 'ENTOURAGE_URL', '"https://api.entourage.social/api/v1/"'
        }
        preprod {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.preprod'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
        }
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
        }
    }

    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/DEPENDENCIES'
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", cloudbeesSonarUrl
        property "sonar.login", cloudbeesSonarLogin
        property "sonar.password", cloudbeesSonarPassword
        property "sonar.jdbc.url", cloudbeesSonarDBUrl
        property "sonar.jdbc.driverClassName", cloudbeesSonarDBDriver
        property "sonar.jdbc.username", cloudbeesSonarDBUsername
        property "sonar.jdbc.password", cloudbeesSonarDBPassword

        property "sonar.projectKey", "entourage"
        property "sonar.projectName", "Entourage Android"
        property "sonar.projectVersion", "${versionMajor}.${versionMinor}.${versionPatch}"
        property "sonar.profile", "Entourage"

        property "sonar.language", "java"
        property "sonar.sources", "src/main/java"
        property "sonar.tests", "src/androidTest/java"
        property "sonar.binaries", "build"
        property "sonar.junit.reportsPath", "build/outputs/androidTest-results/connected"
        property "sonar.jacoco.reportPath", "build/outputs/code-coverage/connected/coverage.ec"
    }
}
