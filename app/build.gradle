apply plugin: "com.android.application"
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    def TEST_ACCOUNT_LOGIN = System.env.TEST_ACCOUNT_LOGIN ? '"' + System.env.TEST_ACCOUNT_LOGIN + '"' : entourageTestLogin
    def TEST_ACCOUNT_PWD = System.env.TEST_ACCOUNT_PWD ? '"' + System.env.TEST_ACCOUNT_PWD + '"' : entourageTestPwd

    defaultConfig {
        applicationId 'social.entourage.android'

        resConfigs 'en', 'fr'

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        // Making either of these two values dynamic in the defaultConfig will
        // require a full APK build and reinstallation because the AndroidManifest.xml
        // must be updated.
        versionCode 602
        versionName "6.2"

        buildConfigField 'String', 'VERSION_FULL_NAME', '"' + rootProject.ext.versionNameProd + '"'
        buildConfigField 'String', 'VERSION_DISPLAY_BRANCH_NAME', '"' + rootProject.ext.versionBranchName + '"'
        setProperty("archivesBaseName", "$appBundleName-$versionNameProd")
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true

        buildConfigField 'String', 'ENTOURAGE_URL', '"https://api.entourage.social/api/v1/"'
        buildConfigField 'String', 'TEST_ACCOUNT_LOGIN', TEST_ACCOUNT_LOGIN
        buildConfigField 'String', 'TEST_ACCOUNT_PWD', TEST_ACCOUNT_PWD
    }

    signingConfigs {
        googleplay {
            keyAlias 'googleplay'
            keyPassword "${System.env.KEYSTORE_PASS}"
            storeFile file('../keystore/googleplay-keystore.jks')
            storePassword "${System.env.KEYSTORE_PASS}"
        }

        debug {
            storeFile file('../keystore/debug.keystore')
        }
    }

    flavorDimensions 'app', 'env'

    productFlavors {
        prod {
            dimension 'env'
        }
        staging {
            dimension 'env'
            applicationIdSuffix '.preprod'

        }
        entourage {
            dimension 'app'
            buildConfigField 'String', 'API_KEY', '"145afedd05482e1e91ed2990"'
            ext {
                prod = [
                        serverURL        : "https://api.entourage.social/api/v1/",
                        deepLinksHostName: "www.entourage.social",
                        deepLinksScheme  : "entourage"
                ]
                staging = [
                        serverURL        : "https://api-preprod.entourage.social/api/v1/",
                        deepLinksHostName: "entourage-landingpages-preprod.herokuapp.com",
                        deepLinksScheme  : "entourage-staging"
                ]
            }
        }
    }

    buildTypes {
        release {
            productFlavors.entourage.signingConfig signingConfigs.googleplay
            debuggable false
        }

        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
            //optimizing build speed
            splits.abi.enable = false
            splits.density.enable = false
            aaptOptions.cruncherEnabled = false
            FirebasePerformance {
                // Set this flag to 'false' to disable @AddTrace annotation processing and
                // automatic monitoring of HTTP/S network requests
                // for a specific build variant at compile time.
                instrumentationEnabled false
            }
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    applicationVariants.all { variant ->
        def flavors = variant.productFlavors
        // flavorDimensions "app" -> 0, "env" -> 1
        def app = flavors[0]
        def environment = flavors[1]

        variant.buildConfigField "String", "ENTOURAGE_URL", "\"${app[environment.name]['serverURL']}\""
        variant.buildConfigField "String", "DEEP_LINKS_SCHEME", "\"${app[environment.name]['deepLinksScheme']}\""
        variant.buildConfigField "String", "DEEP_LINKS_URL", "\"${app[environment.name]['deepLinksHostName']}\""

        variant.mergedFlavor.manifestPlaceholders = [
                deepLinksHostName       : "${app[environment.name]['deepLinksHostName']}",
                deepLinksEntourageScheme: "${app[environment.name]['deepLinksScheme']}"
        ]

        // The defaultConfig values above are fixed, so your incremental builds don't
        // need to rebuild the manifest (and therefore the whole APK, slowing build times).
        // But for release builds, it's okay. So the following script iterates through
        // all the known variants, finds those that are "release" build types, and
        // changes those properties to something dynamic.
        if (variant.buildType.name == "release") {
            variant.outputs.each { output ->
                output.versionCodeOverride = rootProject.ext.versionCodeInt
                output.versionNameOverride = rootProject.ext.versionNameProd
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/DEPENDENCIES'
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }

    dexOptions {
        jumboMode = true
    }

    useLibrary 'android.test.runner'
}

dependencies {
    implementation "androidx.core:core-ktx:1.3.2"
    implementation platform('com.google.firebase:firebase-bom:26.5.0')
    implementation androidSupportDependencies.values()
    implementation devDependencies.values()
    implementation firebaseDependencies.values()
    releaseImplementation firebaseDependenciesRelease.values()
    implementation gmsDependencies.values()
    implementation networkDependencies.values()
    implementation uiDependencies.values()

    entourageImplementation facebookDependencies.values()

    compileOnly javaDependencies.values()

    kapt annotationDependencies.values()

    androidTestImplementation testDependencies.values()
}

apply plugin: "com.google.gms.google-services"
