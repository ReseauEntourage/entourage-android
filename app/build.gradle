apply plugin: "com.android.application"
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    def TEST_PFP_ACCOUNT_LOGIN = System.env.TEST_ACCOUNT_LOGIN ? '"' + System.env.TEST_PFP_ACCOUNT_LOGIN + '"' : pfpTestLogin
    def TEST_PFP_ACCOUNT_PWD = System.env.TEST_ACCOUNT_PWD ? '"' + System.env.TEST_PFP_ACCOUNT_PWD + '"' : pfpTestPwd

    defaultConfig {
        applicationId 'social.entourage.android.pfp'

        resConfigs 'fr'

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode rootProject.ext.versionCodeInt
        versionName rootProject.ext.versionNameProd

        buildConfigField 'String', 'VERSION_DISPLAY_NAME', '"' + rootProject.ext.versionBranchName +'"'
        setProperty("archivesBaseName", "$appBundleName-$versionNameProd")
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true

        buildConfigField 'String', 'API_KEY', '"9ebf2b47e35046e43dae5e31"'

        buildConfigField 'String', 'TEST_ACCOUNT_LOGIN', TEST_PFP_ACCOUNT_LOGIN
        buildConfigField 'String', 'TEST_ACCOUNT_PWD', TEST_PFP_ACCOUNT_PWD

        buildConfigField 'String', 'ENTOURAGE_URL', '"https://apipxtgs.entourage.social/api/v1/"'
        buildConfigField 'String', 'DEEP_LINKS_SCHEME', '"pfp-prod"'
    }

    signingConfigs {
        googleplay_pfp {
            keyAlias 'googleplay-pfp'
            keyPassword "${System.env.KEYSTORE_PASS}"
            storeFile file('../keystore/googleplay-pfp-keystore.jks')
            storePassword "${System.env.KEYSTORE_PASS}"
        }

        debug {
            storeFile file('../keystore/debug.keystore')
        }
    }

    flavorDimensions 'env'

    productFlavors {
        prod {
            dimension 'env'
        }
        staging {
            dimension 'env'
            applicationIdSuffix '.preprod'

            buildConfigField 'String', 'ENTOURAGE_URL', '"https://pfp-back-preprod.herokuapp.com/api/v1/"'
            buildConfigField 'String', 'DEEP_LINKS_SCHEME', '"pfp-staging"'
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.googleplay_pfp
            debuggable false
        }

        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            ext.alwaysUpdateBuildId = false
            ext.enableCrashlytics = false
            //optimizing build speed
            splits.abi.enable = false
            splits.density.enable = false
            aaptOptions.cruncherEnabled = false
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/DEPENDENCIES'
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }

    dexOptions {
        jumboMode = true
    }
}

dependencies {
    implementation androidSupportDependencies.values()
    implementation devDependencies.values()
    implementation firebaseDependencies.values()
    implementation gmsDependencies.values()
    implementation networkDependencies.values()
    implementation uiDependencies.values()

    compileOnly javaDependencies.values()

    annotationProcessor annotationDependencies.values()
}

apply plugin: "com.google.gms.google-services"
