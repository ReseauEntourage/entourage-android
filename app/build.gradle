apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'sonar-runner'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 'git rev-list HEAD --count'.execute([], project.rootDir).text.trim().toInteger()

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.android.support:support-annotations:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.google.android.gms:play-services-maps:7.0.0'
    compile 'com.google.android.gms:play-services-location:7.0.0'
    compile 'com.google.android.gms:play-services-ads:7.0.0'

    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.picasso:picasso:2.5.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.jakewharton:butterknife:6.1.0'

    compile 'com.soundcloud:java-api-wrapper:1.3.1'
    compile 'net.danlew:android.joda:2.7.1'
    compile 'com.appaloosa-store:appaloosa-android-sdk:1.0.4'
    compile files('libs/FlurryAnalytics-5.3.0.jar')

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId 'social.entourage.android'
        minSdkVersion 15
        targetSdkVersion 22
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        
        buildConfigField 'boolean', 'APPALOOSA_AUTO_UPDATE', 'false'
        buildConfigField 'long', 'APPALOOSA_STORE_ID', "3949"
        buildConfigField 'String', 'APPALOOSA_STORE_TOKEN', '"ypt4d26acetzxivf5ynjeeqfisr9p7dr"'
        buildConfigField 'String', 'ENTOURAGE_URL', '"http://entourage-back-preprod.herokuapp.com"'
        buildConfigField 'String', 'FLURRY_API_KEY', "\"${flurryApiKey}\""
        buildConfigField 'String', 'SOUNDCLOUND_SECRET', "\"${soundCloudSecret}\""
        buildConfigField 'String', 'SOUNDCLOUND_ID', "\"${soundCloudId}\""
        buildConfigField 'String', 'SOUNDCLOUND_USER', "\"${soundCloudUser}\""
        buildConfigField 'String', 'SOUNDCLOUND_PASS', "\"${soundCloudPass}\""

    }

    signingConfigs {
        googleplay {
            keyAlias 'googleplay'
            keyPassword entourageKeystorePassword
            storeFile file('../keystore/googleplay-keystore.jks')
            storePassword entourageKeystorePassword
        }

        debug {
            storeFile file('../keystore/debug.keystore')
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.googleplay
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            buildConfigField 'boolean', 'APPALOOSA_AUTO_UPDATE', 'true'
            buildConfigField 'String', 'ENTOURAGE_URL', '"http://entourage-back.herokuapp.com"'
        }
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
        }
    }

    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/DEPENDENCIES'
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", cloudbeesSonarUrl
        property "sonar.login", cloudbeesSonarLogin
        property "sonar.password", cloudbeesSonarPassword
        property "sonar.jdbc.url", cloudbeesSonarDBUrl
        property "sonar.jdbc.driverClassName", cloudbeesSonarDBDriver
        property "sonar.jdbc.username", cloudbeesSonarDBUsername
        property "sonar.jdbc.password", cloudbeesSonarDBPassword

        property "sonar.projectKey", "entourage"
        property "sonar.projectName", "Entourage Android"
        property "sonar.projectVersion", "${versionMajor}.${versionMinor}.${versionPatch}"
        property "sonar.profile", "Entourage"

        property "sonar.language", "java"
        property "sonar.sources", "src/main/java"
        property "sonar.tests", "src/androidTest/java"
        property "sonar.binaries", "build"
        property "sonar.junit.reportsPath", "build/outputs/androidTest-results/connected"
        property "sonar.jacoco.reportPath", "build/outputs/code-coverage/connected/coverage.ec"
    }
}
